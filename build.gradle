buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

version = "r17"
group = "de.ellpeck.naturesaura"
archivesBaseName = "NaturesAura"

if (hasProperty('buildnumber')) {
    version = "${version}-${this.properties['buildnumber']}"
}

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

repositories {
    mavenCentral()
    maven {
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        url = "http://maven.thiakil.com"
    }
    maven {
        url = "https://maven.blamejared.com"
    }
}

dependencies {
    compile "vazkii.patchouli:Patchouli:1.0-19.96"

    deobfCompile "mezz.jei:jei_1.12.2:4.14.4.267"
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"

    compile "com.blamejared:MTLib:3.0.4.8"
    compile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.11.494"
}


minecraft {
    version = "1.12.2-14.23.5.2768"
    mappings = "snapshot_20180720"

    runDir = "run"
    makeObfSourceJar = false

    replaceIn "NaturesAura.java"
    replace "@VERSION@", project.version.toString()
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

apply plugin: 'maven-publish'

jar {
    
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'de/ellpeck/naturesaura/api/**'
}

javadoc {
    include 'de/ellpeck/naturesaura/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
            artifact sourcesJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file:///srv/nginx/maven"
            //url "file:///tmp/maven"
        }
    }
}
